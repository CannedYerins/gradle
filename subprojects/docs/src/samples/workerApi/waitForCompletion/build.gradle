/*
 * Copyright 2017 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import javax.inject.Inject

// The implementation of a single unit of work
class UnitOfWork implements Runnable {
    File fileToReverse
    File destinationFile

    @Inject
    public UnitOfWork(File fileToReverse, File destinationFile) {
        this.fileToReverse = fileToReverse
        this.destinationFile = destinationFile
    }

    @Override
    public void run() {
        destinationFile.text = fileToReverse.text.reverse()
    }
}


class ReverseFiles extends SourceTask {
    @OutputDirectory
    File outputDir

    // The WorkerExecutor will be injected by Gradle at runtime
    @Inject
    WorkerExecutor getWorkerExecutor() {
        throw new UnsupportedOperationException()
    }

    @TaskAction
    void reverseFiles() {
        // START SNIPPET wait-for-completion
        // Create and submit a unit of work for each file
        source.files.each { file ->
            workerExecutor.submit(UnitOfWork.class) { config ->
                config.isolationMode = IsolationMode.NONE
                // Constructor parameters for the unit of work implementation
                config.params = [ file, project.file("${outputDir}/${file.name}") ]
            }
        }

        // Wait for all asynchronous work to complete before continuing
        workerExecutor.await()
        logger.lifecycle("Succesfully processed ${source.files.size()} files")
        // END SNIPPET wait-for-completion
    }
}

task reverseFiles(type: ReverseFiles) {
    outputDir = file("${buildDir}/reversed")
    source("sources")
}
